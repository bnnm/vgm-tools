# uasset.z decompressor by bnnm
#
# barely tested with some Ark file, apparently generated by some version of umodel 
# that (at the time?) didn't support this type of compressed asset.

endian little

idstring "\xC1\x83\x2A\x9E"
idstring "\x00\x00\x00\x00"


get CHUNK_MAX longlong

get DATA_SIZE longlong
get DATA_USIZE longlong

xmath CHUNKS "DATA_USIZE / CHUNK_MAX"
xmath EXTRA "DATA_USIZE % CHUNK_MAX"
if EXTRA != 0
    math CHUNKS += 1
endif


log MEMORY_FILE 0 0
putvarchr MEMORY_FILE DATA_USIZE 0
log MEMORY_FILE 0 0

xmath OFFSET "0x20 + (CHUNKS * 0x10)"

append
for I = 0 < CHUNKS
    get SIZE longlong
    get USIZE longlong

    clog MEMORY_FILE OFFSET SIZE USIZE

    math OFFSET += SIZE
next I
append

get SIZE asize MEMORY_FILE
get NAME basename
log NAME 0 SIZE MEMORY_FILE