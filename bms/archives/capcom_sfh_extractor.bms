# Capcom .SFH extractor by bnnm
#
# kinda useless now since vgmstream can play .sfh directly (in most cases), but may be used for other files


endian big 

idstring "\x00\x53\x46\x48" #\0SFH
get MODE long
get DATA_SIZE long  #clean data size (needed for end padding)
get DUMMY long  #null

if MODE == 0x00010000
    # Devil May Cry 4 Demo (PS3)
    set BLOCK_SIZE long 0x10010
    set SKIP_SIZE 0x10
elif MODE == 0x00010001
    # Jojo's Bizarre Adventure HD (PS3)
    # Sengoku Basara 4 (PS3)
    set BLOCK_SIZE long 0x20000
    set SKIP_SIZE 0x10
else
   print "unknown mode"
   exit
endif


get FILESIZE asize
math FILESIZE -= 0x10 #last part (or more) is garbage

log MEMORY_FILE 0 0

xmath BLOCK_SIZE_MAX "BLOCK_SIZE - SKIP_SIZE"
set OFFSET 0

append #on
do
    #skips garbage on every block
    math OFFSET += SKIP_SIZE

    xmath MAX_COPY " FILESIZE - OFFSET "

    if MAX_COPY > BLOCK_SIZE_MAX
       set MAX_COPY BLOCK_SIZE_MAX
    endif

    log MEMORY_FILE1 OFFSET MAX_COPY

    math OFFSET += MAX_COPY

while OFFSET < FILESIZE
append #off


goto 0 MEMORY_FILE1
get IDSTRING long MEMORY_FILE1

# Sengoku Basara 4 (PS3) uses sfh for various files, usually with the original extension rather than .sfh
if IDSTRING == 0x00434C44 #\0CLD
    string EXT = "dlcp"
elif IDSTRING == 0x53535052 #SSPR
    string EXT = "sspr"
elif IDSTRING == 0x00435241 #\0CRA
    string EXT = "arc"
elif IDSTRING == 0x52494646 #\RIFF
    string EXT = "at3"
else
    string EXT = "bin"
endif    

get BASE_NAME basename
string NAME p= "%s.clean.%s" BASE_NAME EXT

log NAME 0 DATA_SIZE MEMORY_FILE1
