# Platinum Games .pkz extractor by bnnm

idstring "pkzl"
get VERSION long
get SIZE longlong

get FILES long
get START long
get NAMES_SIZE longlong

xmath NAME_TABLE "FILES * 0x20 + START"


for I = 0 < FILES
    #entry 20
    get NAME_OFFSET long
    get TYPE_OFFSET long #>0 is rare (bayo3)
    get FULLSIZE longlong
    get OFFSET longlong
    get SIZE longlong

    savepos CURRENT

    math TYPE_OFFSET += NAME_TABLE
    goto TYPE_OFFSET
    get ALGO string

    if ALGO == "ZStandard"
       comtype "zstd"
    elif ALGO == "None" #seen in .wem packs in Babilon's Fall
       comtype "copy"
    elif ALGO == "OodleKraken" #seen in Bayonetta 3
       comtype "OODLE_kraken"
    else 
       print "Unknown compression"
       exit
    endif

    math NAME_OFFSET += NAME_TABLE
    goto NAME_OFFSET
    get NAME string

    goto CURRENT
  
    #print "entry=%CURRENT|h%, offset=%OFFSET|h% size=%SIZE|h% fullsize=%SIZE|h%"
    clog NAME OFFSET SIZE FULLSIZE 

next I
