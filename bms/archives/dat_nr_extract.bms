# No Reality (PC) .dat extract+decrypt by bnnm

get FILE_SIZE asize
get BASE_NAME basename

get FILES long
get ENTRY_SIZE long
get START_OFFSET long

xmath HEADER_SIZE " FILES * ENTRY_SIZE "

#decrypt header
log MEMORY_FILE 0x0c HEADER_SIZE
callfunction MEMDECRYPT 1 HEADER_SIZE
log MEMORY_FILE2 0 HEADER_SIZE MEMORY_FILE


for COUNT = 0 < FILES
    # get file from header
    get DUMMY long MEMORY_FILE2 #file id?
    get SIZE long MEMORY_FILE2
    get OFFSET long MEMORY_FILE2
    
    string NAME p= "%s__%02i." BASE_NAME COUNT

    # decrypt memory file + extract
    log MEMORY_FILE OFFSET SIZE
    callfunction MEMDECRYPT 1 SIZE
    log NAME 0 SIZE MEMORY_FILE

next COUNT

###

StartFunction MEMDECRYPT
    goto 0 MEMORY_FILE
    for I = 0 < MEMDECRYPT_ARG1
        getvarchr TMP MEMORY_FILE I
        math TMP z 4 #nibble swap
        putvarchr MEMORY_FILE I TMP
    next I
EndFunction
# may be equivalent to:
#encryption math "^z= 4" 8
