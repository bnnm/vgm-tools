# Tecmo's IMG_DB.BIN / GR6.DAT unpacker
# based on the original "project_zero.bms" by aluigi

# see ELF's InitSystemON > cddatInit (games often have full symbols = easy IDA-ing)

set FIX_HXD long 0
set IS_BD_HD long 0
set EXISTS long 0
set EXISTS2 long 0

# Zero (JP)
if EXISTS == 0
    math CD_DAT_TBL     = 0x0
   #math FILE_EXT_TBL   = 0x0
    math FILES          = 1850
    math FILE_DAT       = 0x580348 #?

    open FDSE "IMG_HD.BIN" 1 EXISTS
    open FDSE "SLPS_250.74" 2 EXISTS2

    if EXISTS != 0
    set IS_BD_HD long 1
    endif
endif

# Zero 2 (JP)
if EXISTS == 0
    math CD_DAT_TBL     = 0x1F35F8
   #math FILE_EXT_TBL   = 0x1FFB00
    math FILES          = 4203
    math FILE_DAT       = 0 #?

    open FDSE "SLPS_253.03" 1 EXISTS
    #open FDSE "SLPS_253.03" 2 EXISTS2
    set EXISTS2 long 0
endif

# Gallop Racer 2004 (US)
if EXISTS == 0
    math CD_DAT_TBL     = 0x2262D0
   #math FILE_EXT_TBL   = 0x22F2E8
    math FILES          = 3074
    math FILE_DAT       = 0x9BA5C7

    open FDSE "SLUS_210.31" 1 EXISTS
    open FDSE "SLUS_210.31" 2 EXISTS2
endif

# Gallop Racer 2006 (US)
if EXISTS == 0
    math CD_DAT_TBL     = 0x21FD60
   #math FILE_EXT_TBL   = 0x226C88
    math FILES          = 2371
    math FILE_DAT       = 0

    open FDSE "SLUS_213.93" 1 EXISTS
    #open FDSE "SLUS_213.93" 2 EXISTS2
    set EXISTS2 long 0

    if EXISTS != 0
    set FIX_HXD long 1
    endif
endif


if EXISTS == 0
    print "Error: your version of the game is not supported"
    cleanexit
endif

#--------------------------------------------------------------

# read names in a long string like: "name:index,name:index,...."
putarray 0 FILES 0
if EXISTS2 == 1
    goto FILE_DAT 2
    for I = 0 < FILES

        # check line end (cuts every now and then to avoid giant lines)
        savepos CURRENT
        get SEPARATOR byte 2
        if SEPARATOR == 0x5c #"\"
            get DUMMY byte 2 #0x00
        else
            goto -1 2 SEEK_CUR
        endif


        getct NAME string ':' 2
        getct INDEX string ',' 2
        set KEY long INDEX

        # name is "blah_blah_ext". change to "blah_blah.ext"
        string BASENAME = NAME
        string EXTNAME = NAME
        string BASENAME <= "_"
        string BASENAME -= 1
        string EXTNAME != "_"
        string NAME p= "%s.%s" BASENAME EXTNAME

        putarray 0 KEY NAME
        #print "%NAME% %INDEX%, %KEY%, %I%"
    next I
endif

goto CD_DAT_TBL 1
for I = 0 < FILES
    if IS_BD_HD == 1
        get OFFSET long 1
        get SIZE long 1

        set FLAGS long 0
        math OFFSET *= 0x800
    else
        get OFFSET long 1
        get XSIZE long 1
        get SIZE long 1
    
        xmath FLAGS "OFFSET & 3"
        math OFFSET u>> 2
        math OFFSET * 0x800
    endif
    
    string EXT = ""
    math COUNT = I
    if FIX_HXD != 0
        savepos CURRENT
        
        goto OFFSET
        get ID long
        if ID == 0x48584400
            math COUNT -= 1
            string EXT = "hxd"
        elif ID == 0x00000000
            string EXT = "str" #not all but...
        endif
        
        goto CURRENT
    endif
    
    getarray NAME 0 I
    print "name: %NAME%"
    if NAME == 0 || NAME == ""
        string NAME p "%08d.%s" COUNT EXT
    endif

    if OFFSET > 0 && SIZE > 0
        if FLAGS == 2 || FLAGS == 0
            log NAME OFFSET SIZE
        elif FLAGS == 3
            string NAME p "compressed/%s" NAME
            log NAME OFFSET SIZE
            #clog "" OFFSET SIZE XSIZE
        endif
    endif
next I

print "\nUse the following script on the compressed files:\n  http://aluigi.org/bms/deless.bms"
