# MUL movie demuxer by bnnm
#
# for movies in various Tomb Raider games (use gibbed tools to extract .mul first)

######################

get SRATE long
endian guess SRATE

get FILE_SIZE asize
set OFFSET long 0

math IS_VIDEO = 0
math IS_AUDIO = 0

goto 0x800
get TEST1 long
get TEST2 long
goto 0x2000
get TEST3 long
get TEST4 long
if TEST1 > 0 || TEST2 > 0
    set HEADER_SIZE long 0x800
elif TEST3 > 0 || TEST4 > 0
    set HEADER_SIZE long 0x2000
else
    print "unknown start"
    exit
endif

append #on
do 
    goto OFFSET

    if OFFSET == 0
        math BLOCK_SIZE = HEADER_SIZE
        math IS_VALID = 1
    else
        get BLOCK_TYPE long
        get BLOCK_SIZE long
        
        # TR2013 is padded
        xmath BLOCK_EXTRA "BLOCK_SIZE % 0x10"
        if BLOCK_EXTRA != 0
            xmath BLOCK_PADDING " 0x10 - BLOCK_EXTRA "
            math BLOCK_SIZE += BLOCK_PADDING
        endif
        
        math BLOCK_SIZE += 0x10
        if BLOCK_TYPE == 0
            math IS_VALID = 1
            math IS_AUDIO = 1
        else    
            math IS_VALID = 0
            math IS_VIDEO = 1
        endif
    endif


    if IS_VALID == 1
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    math OFFSET += BLOCK_SIZE

while OFFSET < FILE_SIZE
append #off


if IS_VIDEO == 0
  print "File doesn't contain video"
  cleanexit
endif

if IS_AUDIO == 0
  print "File doesn't contain audio"
  cleanexit
endif

get BASENAME basename
get EXT extension
string NAME p= "%s.audio.%s" BASENAME EXT
get SIZE asize MEMORY_FILE

log NAME 0 SIZE MEMORY_FILE
