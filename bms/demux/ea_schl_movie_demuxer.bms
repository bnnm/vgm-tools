# EA SCHl movie demuxer by bnnm
# for movies that include SCHl or SHxx (.vp6/mad/etc)
# NOTE: only extracts EN audio, modify below for other langs

######################

# set 0 to leave original extension plus an "audio" tag (should work in vgmstream, report otherwise).
# set 1 to rename as .lasf, could use .asf (EA's common default) too but has issues in some players
# set 2 to rename as .lasf and keep original extension
set RENAME_ASF long 2

######################

get FILE_SIZE asize
set OFFSET long 0

append #on
do 
    goto OFFSET

    endian big
    get BLOCK_ID long

    # early Saturn/Mac movies change endian on block
    get BLOCK_SIZE long
    endian guess BLOCK_SIZE

    # early vids seem to end with padding
    if BLOCK_ID == 0 || BLOCK_SIZE == 0
        break
    endif

    # accept standard SCHl
    if BLOCK_ID == 0x5343486C || BLOCK_ID == 0x5343436C || BLOCK_ID == 0x5343446C || BLOCK_ID == 0x53434C6C || BLOCK_ID == 0x5343456C  #"SCHl/SCCl/SCDl/SCLl/SCEl"
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    # accept movie SHxx (EN/FR/GE/IT/SP/RU/JA), default EN
    # for movies with multiaudio uncomment your target language and comment the rest
    if BLOCK_ID == 0x5348454E || BLOCK_ID == 0x5343454E || BLOCK_ID == 0x5344454E || BLOCK_ID == 0x5345454E  #"SHEN/SCEN/SDEN/SEEN"
   #if BLOCK_ID == 0x53484652 || BLOCK_ID == 0x53434652 || BLOCK_ID == 0x53444652 || BLOCK_ID == 0x53454652  #"SHFR/SCFR/SDFR/SEFR"
   #if BLOCK_ID == 0x53484745 || BLOCK_ID == 0x53434745 || BLOCK_ID == 0x53444745 || BLOCK_ID == 0x53454745  #"SHGE/SCGE/SDGE/SEGE"
   #if BLOCK_ID == 0x53484954 || BLOCK_ID == 0x53434954 || BLOCK_ID == 0x53444954 || BLOCK_ID == 0x53454954  #"SHIT/SCIT/SDIT/SEIT"
   #if BLOCK_ID == 0x53485350 || BLOCK_ID == 0x53435350 || BLOCK_ID == 0x53445350 || BLOCK_ID == 0x53455350  #"SHSP/SCSP/SDSP/SESP"
   #if BLOCK_ID == 0x53485255 || BLOCK_ID == 0x53435255 || BLOCK_ID == 0x53445255 || BLOCK_ID == 0x53455255  #"SHRU/SCRU/SDRU/SERU"
   #if BLOCK_ID == 0x53484A41 || BLOCK_ID == 0x53434A41 || BLOCK_ID == 0x53444A41 || BLOCK_ID == 0x53454A41  #"SHJA/SCJA/SDJA/SEJA"
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    math OFFSET += BLOCK_SIZE

while OFFSET < FILE_SIZE
append #off


get BASE_NAME basename
get EXT extension
if RENAME_ASF == 2
    string NAME p= "%s.%s.lasf" BASE_NAME EXT
elif RENAME_ASF == 1
    string NAME p= "%s.audio.lasf" BASE_NAME
else
    string NAME p= "%s.audio.%s" BASE_NAME EXT
endif


get SIZE asize MEMORY_FILE
if SIZE == 0
  print "File doesn't contain audio"
  cleanexit
endif

log NAME 0 SIZE MEMORY_FILE
