# Saturn FILM video extractor by bnnm
# tested with .cak w/ ADX, .cpk w/ raw

# Toshinden URA: extra data after every audio frame
math IS_CPK = 0

endian big

idstring "FILM"
get HEAD_SIZE long
get VERSION long #ex. "1.08" Toshinden URA
get DUMMY long #null

idstring "FDSC"
get CHUNK_SIZE long
math CHUNK_SIZE -= 0x08
getdstring DUMMY CHUNK_SIZE

idstring "STAB"
get CHUNK_SIZE long
get DUMMY long
get ENTRIES long

savepos BASE_OFFSET
math BASE_OFFSET += CHUNK_SIZE
#math BASE_OFFSET = HEAD_SIZE + 0x10


log MEMORY_FILE 0 0
append
for I = 0 < ENTRIES
   get ENTRY_OFFSET long
   get ENTRY_SIZE long
   get ENTRY_INFO1 long
   get ENTRY_INFO2 long

   # probably video frame number
   if ENTRY_INFO1 != 0xFFFFFFFF
      continue
   endif

   # probably number of frames
   if ENTRY_INFO2 != 1
      print "unknown entry found"
      exit
   endif

   math ENTRY_OFFSET += BASE_OFFSET

   if IS_CPK == 1
       math ENTRY_SIZE -= 0x10
   endif 

   log MEMORY_FILE ENTRY_OFFSET ENTRY_SIZE 
next I
append


get BASE_NAME filename #basename
string NAME p= "%s.vgmstream" BASE_NAME

get SIZE asize MEMORY_FILE
log NAME 0 SIZE MEMORY_FILE
