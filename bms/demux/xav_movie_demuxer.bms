# XAVS demuxer by bnnm

idstring "XAVS"
get VIDEO_INFO long

if VIDEO_INFO == 0
    print "File has no video"
    exit
endif

get FILE_SIZE asize
set AUDIO_FRAMES long 0
set HEADER_SIZE long 0x18

log MEMORY_FILE 0 0

append
log MEMORY_FILE 0 HEADER_SIZE

for OFFSET = HEADER_SIZE < FILE_SIZE
    goto OFFSET

    get CHUNK_ID byte
    get CHUNK_SIZE threebyte

    if CHUNK_ID == 0x41 || CHUNK_ID == 0x61 || CHUNK_ID == 0x62 || CHUNK_ID == 0x62 #audio
        math CHUNK_SIZE += 0x04
        log MEMORY_FILE OFFSET CHUNK_SIZE
        math OFFSET += CHUNK_SIZE
        math AUDIO_FRAMES += 1

    elif CHUNK_ID == 0x56 #video
        math CHUNK_SIZE += 0x04
        math OFFSET += CHUNK_SIZE

    elif CHUNK_ID == 0x21 #video?
        math CHUNK_SIZE = 0x04
        math OFFSET += CHUNK_SIZE

    elif CHUNK_ID == 0x5F #eos
        math CHUNK_SIZE = 0x04
        log MEMORY_FILE OFFSET CHUNK_SIZE
        math OFFSET += CHUNK_SIZE

    else
        print "Unknown chunk at %OFFSET|h%"
        exit
    endif


next
append

if AUDIO_FRAMES == 0
    print "File has no audio"
    exit
endif

get FILE_SIZE asize MEMORY_FILE

# fix header
putvarchr MEMORY_FILE 0x04 0 long #width/height
putvarchr MEMORY_FILE 0x08 0 long #related to video
putvarchr MEMORY_FILE 0x10 0 long #related to video


get EXTENSION extension
get BASE_NAME basename
get FILE_SIZE asize MEMORY_FILE
string NAME p= "%s.audio.%s" BASE_NAME EXTENSION

log NAME 0 FILE_SIZE MEMORY_FILE
