# EA WVE (AABB/ad10) .wve/mov demuxer by bnnm
# from Wing Commande 3/4 (PS1), Madden NHL 97 (PC)


get FILESIZE asize
set OFFSET long 0

log MEMORY_FILE 0 0

append #on
do
    goto OFFSET

    endian big
    get BLOCK_ID long

    # some PS1 use BE
    get BLOCK_SIZE long
    endian guess BLOCK_SIZE

    if BLOCK_ID == 0 || BLOCK_SIZE == 0
        break
    endif

    # accept "ad10" "ad11"
    if BLOCK_ID == 0x41643130 || BLOCK_ID == 0x41643131
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif
    
    math OFFSET += BLOCK_SIZE

while OFFSET < FILESIZE
append #off


get NAME basename
get EXT extension
string NAME += ".demux."
string NAME += EXT
get SIZE asize MEMORY_FILE

if SIZE == 0
    print "file has no audio"
    exit
endif

log NAME 0 SIZE MEMORY_FILE
