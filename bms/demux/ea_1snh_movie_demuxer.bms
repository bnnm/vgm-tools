# EA 1SNh movie demuxer by bnnm
# for movies that include 1SNh or SEAD (.tgv/uv/tgq/etc)


######################

# set 0 to leave original extension plus an "audio" tag (should work in vgmstream, report otherwise).
# set 1 to rename as .lasf, could use .asf (EA's common default) too but has issues in some players
# set 2 to rename as .lasf and keep original extension
set RENAME_ASF long 2

######################

get FILE_SIZE asize
set OFFSET long 0

append #on
do
    goto OFFSET

    endian big
    get BLOCK_ID long

    # early Saturn/Mac movies change endian on block
    get BLOCK_SIZE long
    endian guess BLOCK_SIZE

    # early vids seem to end with padding
    if BLOCK_ID == 0 || BLOCK_SIZE == 0
        break
    endif

    # accept standard 1SNh
    if BLOCK_ID == 0x31534E68 || BLOCK_ID == 0x31534E64 || BLOCK_ID == 0x31534E6C || BLOCK_ID == 0x31534E65  #"1SNh/1SNd/1SNl/1SNe"
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    # accept movie values
    if BLOCK_ID == 0x53454144 || BLOCK_ID == 0x534E4443 || BLOCK_ID == 0x53454E44  #"SEAD/SNDC/SEND"
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    math OFFSET += BLOCK_SIZE

while OFFSET < FILE_SIZE
append #off


get BASE_NAME basename
get EXT extension
if RENAME_ASF == 2
    string NAME p= "%s.%s.lasf" BASE_NAME EXT
elif RENAME_ASF == 1
    string NAME p= "%s.audio.lasf" BASE_NAME
else
    string NAME p= "%s.audio.%s" BASE_NAME EXT
endif


get SIZE asize MEMORY_FILE
if SIZE == 0
  print "File doesn't contain audio"
  cleanexit
endif

log NAME 0 SIZE MEMORY_FILE
